// PI_preprocessing.ijm
// Author: Sujin Kim
// Created: 2025-09-26
// Last Modified: 2025-12-08
// Description: PI preprocessing macro with resolution-based background subtraction
//                   and RenyiEntropy thresholding + particle analysis.

// ------------------------------------------------------------
// Background image filenames (customize as exposure time)
// ------------------------------------------------------------
background_1200 = "250710 100 ms PI exposure.jpg";
background_1080 = "250710_100ms_PI_exposure_resized_1920x1080.jpg";

// ------------------------------------------------------------
// Get list of currently open images in Image J or Fiji
// ------------------------------------------------------------
imageTitles = getList("image.titles");

// ------------------------------------------------------------
// Main loop
// ------------------------------------------------------------
for (i = 0; i < imageTitles.length; i++) {
    currentImageTitle = imageTitles[i];

    // Skip if it's a background image
    if (currentImageTitle == background_1200 || currentImageTitle == background_1080)
        continue;

    selectWindow(currentImageTitle);

    // Get resolution
    w = getWidth();
    h = getHeight();

    // ------------------------------------------------------------
    // Background subtraction based on resolution
    // ------------------------------------------------------------
    if (w == 1920 && h == 1200) {
        if (isOpen(background_1200)) {
            imageCalculator("Subtract create", currentImageTitle, background_1200);
        } else {
            print("Background image (1200) not open: " + background_1200);
            continue;
        }

    } else if (w == 1920 && h == 1080) {
        if (isOpen(background_1080)) {
            imageCalculator("Subtract create", currentImageTitle, background_1080);
        } else {
            print("Background image (1080) not open: " + background_1080);
            continue;
        }

    } else {
        print("Unknown resolution: " + w + " x " + h + " for " + currentImageTitle);
        continue;
    }

    // Name of result image generated by ImageJ
    resultImageTitle = "Result of " + currentImageTitle;

    // ------------------------------------------------------------
    // Thresholding + particle analysis
    // ------------------------------------------------------------
    if (isOpen(resultImageTitle)) {
        selectWindow(resultImageTitle);
        run("8-bit");
        run("Auto Threshold", "method=RenyiEntropy white");
        run("Analyze Particles...", "summarize");
        close(); // close result image
    }

    // Close original image
    close(currentImageTitle);
}

// ------------------------------------------------------------
// Show final results table
// ------------------------------------------------------------
if (isOpen("Results"))
    run("Show Results");
